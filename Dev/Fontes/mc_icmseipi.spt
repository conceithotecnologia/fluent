/*==================================================================================================================================
| Empresa..: MultCont Informática                                                                                                  |
| -------------------------------------------------------------------------------------------------------------------------------- |
| Autor....: Jurandy da Silva Costa                                                                                                |
| -------------------------------------------------------------------------------------------------------------------------------- |
| Data.....: 20/01/2006 20:00:00                                        Alterado.: 13/06/2009                                      |
| -------------------------------------------------------------------------------------------------------------------------------- |
| Tipo.....: Stored Procedure                                                                                                      |
| -------------------------------------------------------------------------------------------------------------------------------- |
| Descrição: Calcula Total, Base do ICMS, ICMS, Base do IPI e IPI dos itens de Pedidos, Pré-Notas e Notas Fiscais                  |
| -------------------------------------------------------------------------------------------------------------------------------- |
| Parametros                                                                                                                       |
|  [Entrada]······················································································································ |
             iInformar:  Indica se o usuário digitou o total do item
             iICMS_IPI:  TES indica ICMS sobre o IPI ?
             iCalc_ICM:  TES indica calculo do ICMS ?
             iCalc_IPI:  TES indica calculo do IPI ?
             nUnitario:  Valor Unitário do item
             nQuantos:   Quantidade do item
             nPDescto:   Percentual de Desconto
             nAliqICM:   Alíquota de ICMS
             nReduICM:   Percentual de redução do ICMS
             nAliqIPI:   Alíquota de IPI
             nReduIPI:   Percentual de redução do IPI
             nTotalPro:  Valor Total do item
==================================================================================================================================*/
Create or Replace Function mc_icmseipi####???
( In  iInformar  Integer,
  In  iICMS_IPI  Integer,
  In  iCalc_ICM  Integer,
  In  iCalc_IPI  Integer,
  In  nUnitario  Numeric(15, 3),
  In  nQuantos   Numeric(15, 4),
  In  nPDescto   Numeric(05, 2),
  In  nAliqICM   Numeric(06, 2),
  In  nReduICM   Numeric(06, 2),
  In  nAliqIPI   Numeric(06, 2),
  In  nReduIPI   Numeric(06, 2),
  In  nTotalPro  Numeric(15, 2),
  Out aICMS_IPI  Numeric(15, 2) ARRAY[8] )
As $$

Declare
   nReduz_Base   Numeric(15, 2);               -- Redução na Base de Cálculo
   iTipoLucro    Integer;                      -- Tipo de Lucro 0-Indefinido, 1-Real, 2-Presumido, 3-Simples (Sem ICMS/IPI)
-- aICMS_IPI     Numeric(15, 2) ARRAY[8];      -- aICMS_IPI[1] = Base de Cálculo do ICMS
                                               -- aICMS_IPI[2] = Alíquota do ICMS
                                               -- aICMS_IPI[3] = Valor do ICMS
                                               -- aICMS_IPI[4] = Base de Cálculo do IPI
                                               -- aICMS_IPI[5] = Alíquota do IPI
                                               -- aICMS_IPI[6] = Valor do IPI
                                               -- aICMS_IPI[7] = Total do Produto (Quanto * Unitário)
                                               -- aICMS_IPI[8] = Total do Item (Produto + IPI)

Begin
   aICMS_IPI[1] := 0;                          -- Base de Cálculo do ICMS
   aICMS_IPI[2] := nAliqICM;                   -- Alíquota do ICMS
   aICMS_IPI[3] := 0;                          -- Valor do ICMS
   aICMS_IPI[4] := 0;                          -- Base de Cálculo do IPI
   aICMS_IPI[5] := nAliqIPI;                   -- Alíquota do IPI
   aICMS_IPI[6] := 0;                          -- Valor do IPI
   aICMS_IPI[7] := nTotalPro;                  -- Total do Produto (Quanto * Unitário)
   aICMS_IPI[8] := 0;                          -- Total do Item (Produto + IPI)

   -- Calcula o total do item se o usuário não informar
   If iInformar < 1 Or iInformar Is Null Then
      aICMS_IPI[7] := Round(nQuantos * nUnitario * (1 - nPDescto / 100), 2);
   End If;
   -- Busca o Tipo de Lucro no cadastro de filiais
   Select tipo_lucro Into iTipoLucro
     From [(ss063)] Where filial = ???;
   -- Cálculo do IPI
   aICMS_IPI[4] := aICMS_IPI[7];
   nReduz_Base  := 0;
   If iCalc_IPI = 1 And iTipoLucro <> 3 Then
      -- Aplica a Redução da Base de Calculo do IPI
      If nReduIPI > 0 Then
         nReduz_Base := Round(aICMS_IPI[4] * nReduIPI / 100, 2);
      End If;
      aICMS_IPI[4] := aICMS_IPI[4] - nReduz_Base;
      aICMS_IPI[6] := Round(aICMS_IPI[4] * aICMS_IPI[5] / 100, 2);
   Else
      -- Zera Base, Alíquota informada e Imposto caso a TES não permita IPI
      aICMS_IPI[4] := 0;
      aICMS_IPI[5] := 0;
      aICMS_IPI[6] := 0;
   End If;

   -- Calcula do ICMS
   aICMS_IPI[1] := aICMS_IPI[7];
   nReduz_Base  := 0;
   If iCalc_ICM = 1 And iTipoLucro <> 3 Then
      If iICMS_IPI = 1 Then
         aICMS_IPI[1] := aICMS_IPI[1] + aICMS_IPI[6];
      End If;
      -- Aplica a Redução da Base de Calculo do ICMS
      If nReduICM > 0 Then
         nReduz_Base := Round(aICMS_IPI[1] * nReduICM / 100, 2);
      End If;
      aICMS_IPI[1] := aICMS_IPI[1] - nReduz_Base;
      aICMS_IPI[3] := Round(aICMS_IPI[1] * aICMS_IPI[2] / 100, 2);
   Else
      -- Zera Base, Alíquota informada e Imposto caso a TES não permita ICMS
      aICMS_IPI[1] := 0;
      aICMS_IPI[2] := 0;
      aICMS_IPI[3] := 0;
   End If;
   aICMS_IPI[8] := aICMS_IPI[7] + aICMS_IPI[6];
end;
$$ language 'plpgsql'
